# Plane across a cloudy sky

display width 1920
display height 1080

scene yellow-plane
    begin
        load yellow-plane.png
        log yellow plane - https://commons.wikimedia.org/wiki/File:Model_plane_flying_at_Bonchester_Heights_-_geograph.org.uk_-_5627871.jpg
        place yellow-plane named plane1 at -200,200,50 scale 50
        set blur of plane1 to 10
        darken plane1 to 40
        move plane1 to 2300, 600 in 30 seconds
        scale plane1 to 100, 100 in the same time
        darken plane1 to 10 in the same time
        speed plane1 to 120 in the same time
        set blur of plane1 to 0 in 5 seconds

    when ($plane1.x > 1400)
        rotate plane1 by -30 in 5 seconds

    after 1 minute
        stop
end scene

scene grey-planes
    begin
        load grey-plane.png
        log grey plane https://commons.wikimedia.org/wiki/File:Model_plane_flying_at_Bonchester_Heights_-_geograph.org.uk_-_5627870.jpg
        place grey-plane named grey1 at 400,400,100
        place grey-plane named grey2 at 600,600,110
        place grey-plane named grey3 at 500,500,115
        create group grey-planes
        add to group grey-planes grey1 grey2 grey3
        place grey-planes at 200,200,10 scale 30
        move grey-planes to 1000,0 in 10 seconds
        move grey3 to 1200,500 in 10 seconds
end scene

# main scene
    begin
        load evening-sky.png named sky
        load foreground.png
        put sky as background
        put foreground as ground depth 10
        window background at $CENTREX, $CENTREY, $WIDTH, $HEIGHT
        move window background to 5277, $CENTREY in 3 minutes
        start grey-planes
        create text hello
        set content of hello to Hello World!
        set font color of hello to blue
        set font size of hello to 60
        set font style of hello to underline
        place hello at 500,500,0

after 3 seconds
    start yellow-plane
